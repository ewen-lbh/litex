// Concatenation implies optional whitespace
// concantenation with . implies no whitespace
// concatenation with ~ implies required whitespace

expression ::= quantification | relationship | atom | operation
quantification ::= quantifier~expression
quantifier ::= ("∀" | "AA" | "forall" | ("∃" | "EE" | "exists")."!"?).decorations
relationship ::= expression~relation~expression
operation ::= expression~binary_operator~expression | prefix_operator.expression | atom.postfix_operator | big_operator.decorations expression
atom ::= group | (name | quantity | "+" | "-" | text).decorations 
group ::= "{" expression "}" | "(" expression ")" | "[" expression "]" | "<".expression.">" | "|" expression "|" | "||" expression "||" | "[|" expression "|]" 
big_operator ::= "Sum" | "sum" | "Product" | "product" | "Coproduct" | "Union" | "Intersection" | "Directsum" | "Oplus" | "∑"
binary_operator ::= ([+×*·⊕°,] | "inter" | "union" | "_|_" | "bot" | "perp" | "orthog").decorations
prefix_operator ::= "+" | "-" | "^c" | "^t"
postfix_operator ::= "!" | "->" | "^T" | "^_|_" | "^bot" | "^orthog" | "^perp"
relation ::= ([~=←→↑↓≡≈≅><≥≤⇒⇐⇔⊂⊃∈] | "->" | "|->" | "<-|" | "<-" | "==>" | "<==" | ">=" | "<=" | "<=>" | "<==def==>" | "subset" | "superset" | "in" | "element" "of"? | "includes" | "included" "in"? | "--".atom."->").decorations
decorations ::= ("__".atom)?.("^^".atom)?.("_".atom)?.("^".atom)?
quantity ::= number (~unit.decorations (operator unit.decorations)*)?
number ::= digit+
unit ::= ([nµmcdhkMGTP] | "da")?.("°C" | "°F" | "°f" | "°" | "rad" | "mHg" | "m" | "g" | "mol" | "s" | "K" | "L" | "F" | "H" | "eV" | "lux" | "A" | "V" | "Ohm" | "Ω" | "bar" | "atm" | "Pa" | "J" | "W")
text ::= font_specifier?.quoted_string
font_specifier ::= [fckoibms]
