// Concatenation implies optional whitespace
// concantenation with . implies no whitespace
// concatenation with ~ implies required whitespace
// TODO function calls
// TODO floats with french ',' alongside '.'
// TODO * as a postfix operator for ℝ*, with name ::KleeneStar and not ::Product. ℝ^* should also work, with * parsed as BareOperator::KleeneStar. To have ^\cdot, you'll have to either use:
// TODO latex escape hatch as font command: tex"\cdot" renders as \cdot (and therefore do ^tex"\cdot") or ^· (with · parsed as BareOperator::CenterDot)
/* TODO(notsure) context-sensitive parsing of * into ::Product or ::CartesianProduct. 
    Or use xx for cartesian products? no good answer here... 
    maybe when x is alone and surrounded by atoms ? 
    but then 8 + x - 3 would  technically render to 8 + × - 3, since bare operators are atoms... 
    maybe atoms but without bare operators? 
    expectation is that if you implicit-multiply sth by x, you'll not put a space before it... not sure about it.
*/

expression ::= quantification | relationship | atom | operation
quantification ::= quantifier~expression
quantifier ::= ("∀" | "AA" | "forall" | ("∃" | "EE" | "exists")."!"?).decorations
relationship ::= expression~relation~expression
operation ::= expression~binary_operator~expression | prefix_operator.expression | atom.postfix_operator | big_operator.decorations expression
atom ::= group | (name | quantity | bare_operator | text).decorations 
group ::= "{" expression "}" | "(" expression ")" | "[" expression "]" | "<".expression.">" | "|" expression "|" | "||" expression "||" | "[|" expression "|]" 
bare_operator ::= "+" | "-" | "*" | // | ... (operators (whatever type) that can reasonably used as an exponent/subscript of a variable, e.g. lim(x->0⁺) or f₋ or ℝ* (yes, * is already superscript-y in regular typography, but widely accepted as a quick-to-type substitute to mean · or ×)
big_operator ::= "Sum" | "sum" | "Product" | "product" | "Coproduct" | "Union" | "Intersection" | "Directsum" | "Oplus" | "∑" // | ...
binary_operator ::= ([+×*·⊕°,] | "inter" | "union" | "_|_" | "bot" | "perp" | "orthog").decorations // | ...
prefix_operator ::= "+" | "-" | "^c" | "^t" // | ...
postfix_operator ::= "!" | "->" | "^T" | "^_|_" | "^bot" | "^orthog" | "^perp" // | ...
relation ::= ([~=←→↑↓≡≈≅><≥≤⇒⇐⇔⊂⊃∈] | "->" | "|->" | "<-|" | "<-" | "==>" | "<==" | ">=" | "<=" | "<=>" | "<==def==>" | "subset" | "superset" | "in" | "element" "of"? | "includes" | "included" "in"? | "--".atom."->").decorations // | ...
decorations ::= ("__".atom)?.("^^".atom)?.("_".atom)?.("^".atom)?
quantity ::= number (~unit.decorations (operator unit.decorations)*)?
number ::= digit+
unit ::= ([nµmcdhkMGTP] | "da")?.("°C" | "°F" | "°f" | "°" | "rad" | "mHg" | "m" | "g" | "mol" | "s" | "K" | "L" | "F" | "H" | "eV" | "lux" | "A" | "V" | "Ohm" | "Ω" | "bar" | "atm" | "Pa" | "J" | "W") // | ...
text ::= font_specifier?.quoted_string
font_specifier ::= [fckoibms]
